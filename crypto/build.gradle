plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlinx-serialization'
}

android {
    namespace 'com.blockstream.crypto'
    compileSdk 33

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 33

        consumerProguardFiles "consumer-rules.pro"
    }
    compileOptions {
        sourceCompatibility = "11"
        targetCompatibility = "11"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }
}

kotlin {
    jvmToolchain(11)
}

task fetchBinaries {
    doFirst{
        def jniLibs = new File('./crypto/src/main/jniLibs')
        if (!jniLibs.exists()) {
            exec {
                commandLine './fetch_gdk_binaries.sh'
            }
        }else{
            print "-- Skipped --"
        }
    }
    outputs.upToDateWhen { false }
}

afterEvaluate {
    android.libraryVariants.all { variant ->
        variant.getPreBuildProvider().configure { dependsOn(fetchBinaries) }
    }
}

dependencies {
    /**  --- Kotlin & KotlinX ------------------------------------------------------------------- */
    implementation libs.kotlinx.serialization.core
    implementation libs.kotlinx.serialization.json
    implementation(libs.kotlinx.datetime)
    /** ----------------------------------------------------------------------------------------- */
    
    /**  --- AndroidX --------------------------------------------------------------------------- */
    implementation libs.androidx.annotation
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Lifecycle -------------------------------------------------------------------------- */
    implementation libs.androidx.lifecycle.livedata.ktx
    /** ----------------------------------------------------------------------------------------- */

    /**  --- RxJava  ---------------------------------------------------------------------------- */
    implementation "io.reactivex.rxjava3:rxjava:3.1.2"
    implementation "io.reactivex.rxjava3:rxkotlin:3.0.1"
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Logging ---------------------------------------------------------------------------- */
    implementation libs.slf4j.simple
    implementation libs.kotlin.logging.jvm
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Testing ---------------------------------------------------------------------------- */
    testImplementation libs.junit
    /** ----------------------------------------------------------------------------------------- */
}