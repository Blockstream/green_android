plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlinx-serialization'
}

android {
    namespace 'com.blockstream.crypto'
    compileSdkVersion 33
    buildToolsVersion "33.0.0"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 33

        consumerProguardFiles "consumer-rules.pro"
    }
    compileOptions {
        sourceCompatibility = "11"
        targetCompatibility = "11"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

kotlin {
    jvmToolchain(11)
}

task fetchBinaries {
    doFirst{
        def jniLibs = new File('./crypto/src/main/jniLibs')
        if (!jniLibs.exists()) {
            exec {
                commandLine './fetch_gdk_binaries.sh'
            }
        }else{
            print "-- Skipped --"
        }
    }
    outputs.upToDateWhen { false }
}

afterEvaluate {
    android.libraryVariants.all { variant ->
        variant.getPreBuildProvider().configure { dependsOn(fetchBinaries) }
    }
}

dependencies {
    /**  --- Kotlin & KotlinX ------------------------------------------------------------------- */
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:$kotlinx_serialization_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinx_serialization_version"
    implementation("org.jetbrains.kotlinx:kotlinx-datetime:0.4.0")
    /** ----------------------------------------------------------------------------------------- */
    
    /**  --- AndroidX --------------------------------------------------------------------------- */
    implementation 'androidx.annotation:annotation:1.6.0'
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Lifecycle -------------------------------------------------------------------------- */
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.6.0"
    /** ----------------------------------------------------------------------------------------- */

    /**  --- RxJava  ---------------------------------------------------------------------------- */
    implementation "io.reactivex.rxjava3:rxjava:3.1.2"
    implementation "io.reactivex.rxjava3:rxkotlin:3.0.1"
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Logging ---------------------------------------------------------------------------- */
    implementation "org.slf4j:slf4j-simple:$slf4j_android"
    implementation "io.github.microutils:kotlin-logging-jvm:$kotlin_logging"
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Testing ---------------------------------------------------------------------------- */
    testImplementation 'junit:junit:4.13.2'
    /** ----------------------------------------------------------------------------------------- */
}