apply plugin: 'com.android.application'

android {
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/INDEX.LIST'
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
        exclude 'org/bitcoinj/crypto/mnemonic/wordlist/english.txt'
        exclude 'org/bitcoinj/crypto/cacerts'
        exclude 'org/codehaus/jackson/impl/VERSION.txt'
        exclude 'org/codehaus/jackson/map/VERSION.txt'
        exclude 'org/fusesource/leveldbjni/version.txt'
        exclude 'org/h2/util/data.zip'
        exclude 'META-INF/native/linux32/libleveldbjni.so'
        exclude 'META-INF/native/linux64/libleveldbjni.so'
        exclude 'META-INF/native/osx/libleveldbjni.jnilib'
        exclude 'META-INF/native/windows32/leveldbjni.dll'
        exclude 'META-INF/native/windows64/leveldbjni.dll'
        exclude 'META-INF/services/java.sql.Driver'
        exclude 'com/google/thirdparty/publicsuffix/PublicSuffixPatterns.gwt.xml'
        exclude 'com/google/thirdparty/publicsuffix/PublicSuffixType.gwt.xml'
        exclude 'com/mysql/jdbc/Charsets.properties'
        exclude 'com/mysql/jdbc/LocalizedErrorMessages.properties'
        exclude 'com/mysql/jdbc/TimeZoneMapping.properties'
        exclude 'com/mysql/jdbc/configs/3-0-Compat.properties'
        exclude 'com/mysql/jdbc/configs/5-0-Compat.properties'
        exclude 'com/mysql/jdbc/configs/clusterBase.properties'
        exclude 'com/mysql/jdbc/configs/coldFusion.properties'
        exclude 'com/mysql/jdbc/configs/fullDebug.properties'
        exclude 'com/mysql/jdbc/configs/maxPerformance.properties'
        exclude 'com/mysql/jdbc/configs/solarisMaxPerformance.properties'
    }
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        applicationId 'com.greenaddress.greenbits_android_wallet'
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 338
        // Don't use defaultConfig.getProperty("versionCode") in versionName
        // because it breaks F-Droid!
        versionName "3.3.8"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    flavorDimensions 'normal'
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            multiDexEnabled true
            minifyEnabled true
            shrinkResources true
            matchingFallbacks = ['normal']
        }
        debug {
            multiDexEnabled true
            matchingFallbacks = ['normal']
        }
    }
    splits {
        abi {
            enable false
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk true
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride = 22000000 + android.defaultConfig.versionCode
        }
    }
    defaultConfig{
        vectorDrawables.useSupportLibrary = true
    }
    productFlavors {
        production {
        }
        production_21 {
            minSdkVersion 21
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
        ignoreWarnings false
        disable 'MissingTranslation'
    }
    task fetchdepstask (type: Exec) {
        workingDir '.'
        commandLine './fetch_gdk_binaries.sh'
    }
}

afterEvaluate {
  android.applicationVariants.all { variant ->
      def folder = new File('./app/src/main/jniLibs')
      if (!folder.exists()) {
          variant.javaCompiler.dependsOn(fetchdepstask)
      }
  }
}

configurations {
    implementation.exclude group: "org.json", module: "json"
}

dependencies {
    implementation project(':bitcoinj:core')

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "androidx.preference:preference:1.1.0"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'com.google.zxing:core:3.3.3' // API <=24 compatibility
    //noinspection GradleDependency
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-json-org:2.9.6'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.8'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // used to redirect bitcoinj logging to android in debug builds, enable manually
    // debugApi 'com.noveogroup.android:android-logger:1.3.6'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

}

repositories {
    google()
    jcenter()
}
