plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'kotlinx-serialization'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.adarshr.test-logger' version '2.1.1'
}

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 378
        versionName "3.7.8"
        setProperty("archivesBaseName", "BlockstreamGreen" + "-v" + versionName)

        // Export schemas
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }

        testApplicationId "com.blockstream.green.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "normal"
    productFlavors {
        development {
            applicationId "com.greenaddress.greenbits_android_wallet.dev"
            versionNameSuffix "-dev" + appendGdkCommitHash()
            resValue "string", "app_name", "Green Dev"
            resValue "string", "application_id", applicationId
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher_dev",
                    appIconRound: "@mipmap/ic_launcher_round_dev"
            ]
        }

        production {
            applicationId "com.greenaddress.greenbits_android_wallet"
            resValue "string", "app_name", "Green"
            resValue "string", "application_id", applicationId
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher",
                    appIconRound: "@mipmap/ic_launcher_round"
            ]
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride = 22000000 + android.defaultConfig.versionCode
        }
    }
    buildFeatures {
        dataBinding true
    }
    buildTypes {
        release {
            multiDexEnabled true
            minifyEnabled true
            shrinkResources true
            matchingFallbacks = ['normal']
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    splits {
        abi {
            enable false
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    lintOptions {
        abortOnError false
        ignoreWarnings false
        disable 'MissingTranslation'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
}

// Configure kapt to correct error types by setting correctErrorTypes to true
kapt {
    correctErrorTypes true
}

testlogger {
    theme 'mocha'
}
dependencies {
    /**  --- Modules ---------------------------------------------------------------------------- */
    implementation project(':hardware')
    implementation project(':crypto')
    implementation project(':app')
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Kotlin & KotlinX ------------------------------------------------------------------- */
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:$kotlinx_serialization_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinx_serialization_version"
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Android / Google ------------------------------------------------------------------- */
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "androidx.appcompat:appcompat:1.4.0"
    implementation "com.google.android.material:material:1.4.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.2"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.activity:activity-ktx:1.4.0"
    implementation "androidx.biometric:biometric:1.2.0-alpha04"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.browser:browser:1.4.0"
    implementation("androidx.recyclerview:recyclerview:1.2.1")
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Lifecycle -------------------------------------------------------------------------- */
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.0"
    implementation "androidx.lifecycle:lifecycle-process:2.4.0"
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Navigation ------------------------------------------------------------------------- */
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.5"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.5"
    testImplementation "androidx.navigation:navigation-testing:2.3.5"
    androidTestImplementation "androidx.navigation:navigation-testing:2.3.5"
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Room ------------------------------------------------------------------------------- */
    implementation "androidx.room:room-runtime:2.3.0"
    implementation "androidx.room:room-rxjava3:2.3.0"
    kapt "androidx.room:room-compiler:2.3.0"
    implementation "androidx.room:room-ktx:2.3.0"
    testImplementation "androidx.room:room-testing:2.3.0"
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Hilt Dependency Injection  --------------------------------------------------------- */
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // For instrumentation tests
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"

    // For local unit tests
    testImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptTest "com.google.dagger:hilt-android-compiler:$hilt_version"
    /** ----------------------------------------------------------------------------------------- */

    /**  --- RxJava  ---------------------------------------------------------------------------- */
    implementation "io.reactivex.rxjava3:rxjava:3.0.12"
    implementation "io.reactivex.rxjava3:rxkotlin:3.0.1"
    implementation "io.reactivex.rxjava3:rxandroid:3.0.0"
    implementation "com.jakewharton.rxbinding4:rxbinding:4.0.0"
    implementation "com.github.akarnokd:rxjava3-bridge:3.0.0"
    /** ----------------------------------------------------------------------------------------- */

    /**  --- FastAdapter  ----------------------------------------------------------------------- */
    implementation "com.mikepenz:fastadapter:5.6.0"
    implementation "com.mikepenz:fastadapter-extensions-diff:5.6.0" // diff util helpers
    implementation "com.mikepenz:fastadapter-extensions-binding:5.6.0" // view binding helpers
    implementation "com.mikepenz:fastadapter-extensions-expandable:5.6.0"
    implementation "com.mikepenz:fastadapter-extensions-ui:5.6.0" // pre-defined ui components
    implementation "com.mikepenz:fastadapter-extensions-scroll:5.6.0"
    implementation "com.mikepenz:fastadapter-extensions-utils:5.6.0"
    implementation "com.mikepenz:itemanimators:1.1.0"
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Logging ---------------------------------------------------------------------------- */
    implementation 'org.slf4j:slf4j-android:1.7.30'
    implementation 'io.github.microutils:kotlin-logging:1.12.0'
    /** ----------------------------------------------------------------------------------------- */

    /**  --- QR Scanner ------------------------------------------------------------------------- */
    implementation('com.journeyapps:zxing-android-embedded:4.1.0') { transitive = false }
    implementation 'com.google.zxing:core:3.3.3' // API <= 24 compatibility
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Jackson ---------------------------------------------------------------------------- */
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-json-org:2.9.6'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.9.6'
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Beagle  ---------------------------------------------------------------------------- */
    developmentImplementation "com.github.pandulapeter.beagle:ui-drawer:2.6.5"
    developmentImplementation "com.github.pandulapeter.beagle:log-crash:2.6.5"

    productionImplementation "com.github.pandulapeter.beagle:noop:2.6.5"
    productionImplementation "com.github.pandulapeter.beagle:log-crash-noop:2.6.5"
    /** ----------------------------------------------------------------------------------------- */

    testImplementation fileTree(dir: 'libs', include: ['.jar', '.so'])

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.0'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:3.2.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

def appendGdkCommitHash () {
    def cmd = "git --git-dir=crypto/gdk/.git rev-parse --short HEAD"
    def proc = cmd.execute ()
    def hash = proc.text.trim()
    hash ? ("-gdk:" + hash) : ""
}